/*
    Some additional files are required to build the project:
    1. Google configuration from
        https://console.firebase.google.com/u/0/project/smailer-24874/settings/general/android:com.bopr.android.smailer
        should be put into app/google-services.json
    2.  Signing keystore should be put into/app/keystore.p12
    3.  (Optional) Authentication data for debugging should be put into /app/src/debug/assets/debug.properties

    To run debug flavor add SHA-1 fingerprint from
    <user_dir>/.android/debug.keystore (password "android") to  api console:
    https://console.developers.google.com/apis/credentials/oauthclient/376904884028-f0m6ki37c8b4cf93aktk0jgag3tiu922.apps.googleusercontent.com?project=smailer-24874
*/

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.google-services'
apply from: 'user.gradle'
apply from: 'local.gradle'

android {
    compileSdkVersion 29
    buildToolsVersion '28.0.3'

    signingConfigs {
        config {
            keyAlias 'sample'
            storeFile file('keystore.p12')
            storePassword store_password
            keyPassword key_password
        }
    }

    defaultConfig {
        applicationId 'com.bopr.android.smailer'
        minSdkVersion 18
        targetSdkVersion 29
        versionCode 66
        versionName '1.6.7.1'
        archivesBaseName = "smailer-${versionName}"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        all {
//            minifyEnabled true /* NOTE: obfuscation may break everything! do test app in release mode! */
//            shrinkResources true
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.config
        }
        debug {
//            testCoverageEnabled = true
        }
    }

    flavorDimensions "main"

    productFlavors {
        free {
            dimension "main"
        }
        paid {
            dimension "main"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        pickFirst 'META-INF/LICENSE.txt' /* JavaMail license file*/
        exclude 'META-INF/DEPENDENCIES'  /* resolves conflict in gmail package */
    }
    compileOptions {
        sourceCompatibility = 1.7
        targetCompatibility = 1.7
    }

    configurations {
        all {
            exclude group: 'org.apache.httpcomponents'
            exclude group: 'commons-logging', module: 'commons-logging'
            exclude group: 'com.google.guava', module: 'listenablefuture'
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'android.arch.work:work-runtime:1.0.1'
    implementation 'com.sun.mail:android-mail:1.6.4'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation 'com.google.api-client:google-api-client-android:1.30.7'
    implementation 'com.google.apis:google-api-services-gmail:v1-rev98-1.25.0'
    implementation 'com.google.apis:google-api-services-drive:v3-rev99-1.23.0'

    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'com.github.tony19:logback-android-core:1.1.1-6'
    implementation('com.github.tony19:logback-android-classic:1.1.1-6') {
        exclude group: 'com.google.android'
    }

    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }

    freeImplementation 'com.google.android.gms:play-services-ads:18.3.0'

    /* NOTE: uninstall all application's apk-s from device before running tests.
       they may interfere and lead to unpredictable results */

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'org.mockito:mockito-core:2.7.22'

    androidTestImplementation 'junit:junit:4.13'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}