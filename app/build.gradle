apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply from: 'user.gradle'
apply from: 'local.gradle'

android {
    compileSdkVersion 28
    signingConfigs {
        config {
            keyAlias 'sample'
            storeFile file('../etc/keystore.jks')
            storePassword store_password
            keyPassword key_password
        }
    }

    defaultConfig {
        applicationId 'com.bopr.android.smailer'
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 37
        versionName '1.2.6'
        archivesBaseName = "smailer-${versionName}"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    lintOptions {
        abortOnError true
    }

    buildTypes {
        release {
            /* NOTE: minification may break everything! do test app in release mode */
//            minifyEnabled true
//            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            multiDexEnabled true
            testCoverageEnabled = true
        }
    }

    flavorDimensions "main"

    productFlavors {
        free {
            dimension "main"
        }
        paid {
            dimension "main"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        pickFirst 'META-INF/LICENSE.txt' // JavaMail license file
    }

    buildToolsVersion '28.0.3'
}

repositories {
    maven { url 'https://maven.java.net/content/groups/public/' }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:preference-v7:28.0.0'
    implementation 'com.android.support:preference-v14:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.sun.mail:android-mail:1.5.5'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android-core:1.1.1-6'
    implementation ('com.github.tony19:logback-android-classic:1.1.1-6') {
        exclude group: 'com.google.android'
    }
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.8@aar') {
        transitive = true
    }

    freeImplementation 'com.google.android.gms:play-services-ads:17.1.2'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.0'
    testImplementation 'org.mockito:mockito-core:2.7.22'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}